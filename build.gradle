buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath 'com.github.Jartreg.gradle-apt-repo:gradle-apt-repo:master-SNAPSHOT'
    }
}

plugins {
    id 'base'
}

apply plugin: 'me.jartreg.apt-repo'

// Apply the base plugin to all subprojects
subprojects {
    apply plugin: 'base'
}

task aptRepo(type: AptRepository) {
    distribution('stable') {
        origin = "Technik-AG"
        description = "Die APT-Repository der Technik-AG"

        component('main') {
            subprojects {
                if(!path.startsWith(":packages:"))
                    return;

                include tasks.getByPath("${path}:getPackage").outputs.files.filter {
                    it.isFile() && it.name.endsWith(".deb")
                }
            }
        }
    }

    signing {
        keyRingFile = 'keyring.gpg'
        keyId = System.env.SIGNING_KEY_ID
        passphrase = System.env.SIGNING_PASSPHRASE
    }
}
assemble.dependsOn aptRepo

task copyRepoPackage(type: Copy) {
    mustRunAfter aptRepo
    
    from tasks.getByPath(':packages:t-ag-apt-repository:getPackage')
    rename { 'repo.deb' }
    into "${buildDir}/apt-repository"
}
assemble.dependsOn copyRepoPackage

task copyReadme(type: Copy) {
    from 'README.md'
    into "${buildDir}/apt-repository"
}
assemble.dependsOn copyReadme